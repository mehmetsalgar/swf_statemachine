grammar org.salgar.swf_statemachine.xtext.StateMachineDsl with org.eclipse.xtext.common.Terminals

generate stateMachineDsl "http://www.salgar.org/swf_statemachine/xtext/StateMachineDsl"

Model:
	(elements+=Base)*
;

PackageDeclaration:
	'package' name = QualifiedName '{'
		(elements += Base)*
	'}'
;

StateMachine:
	'statemachine' name=ID  'initialState' initialState = [State]
	'control-object' controlObject = ControlObject
	('events' ('{'
		(events += Event)*
	'}'))
	('states' ('{'
		(states += State)*
	'}'))
;

Base: PackageDeclaration | StateMachine;

QualifiedName:
	ID ('.' ID)*
;

State:
	'state' name=ID
	'{'
		(transitions += Transition)*
	'}'
;

Transition:
	'transition' name=ID
	'=>' target = [State]
	'{'
		('trigger' '{' trigger = [Event] '}')
		('guard' '{' guard = Guard '}')?
		('action' '{' action = Action '}')?
	'}'
;

Event:
	'event' name=ID
;

Guard: {Guard}
	name=ID
;

Action: {Action}
	name=ID
;

ControlObject:
	'{'
		(attributes+=ControlObjectAttribute)*
	'}'
;

ControlObjectAttribute:
	'attribute' name=ID
	('type' '=' (
			(type=AttributeBase)
		)
	)
;

AttributeBase: ObjectType | SimpleType
;

SimpleType: type=InternalType
;

ObjectType: type=STRING
;

enum InternalType:
	NONE = 'NONE' | BOOLEAN = 'java.lang.Boolean' | INTEGER = 'java.lang.Integer' | LONG = 'java.lang.Long' | FLOAT = 'java.lang.Float' | DECIMAL = 'java.lang.Decimal' |
	STRING = 'java.lang.String' | DATE = 'java.lang.Date' | DATETIME = 'java.lang.DateTime' | TIME = 'java.lang.Time' | LIST = 'java.util.List'
;

